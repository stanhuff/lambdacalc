tru = λt. λf. t;
fls = λt. λf. f;
test = λl. λm. λn. l m n;
and = λb. λc. b c fls;
or = λb. λc. b tru c;
not = λb. b fls tru;
pair = λf. λs. λb. b f s;
fst = λp. p tru;
snd = λp. p fls;

c0 = λs. λz. z;
c1 = λs. λz. s z;
c2 = λs. λz. s (s z);
c3 = λs. λz. s (s (s z));
c4 = λs. λz. s (s (s (s z)));
c5 = λs. λz. s (s (s (s (s z))));
c6 = λs. λz. s (s (s (s (s (s z)))));
c7 = λs. λz. s (s (s (s (s (s (s z))))));
c8 = λs. λz. s (s (s (s (s (s (s (s z)))))));
c9 = λs. λz. s (s (s (s (s (s (s (s (s z))))))));

scc = λn. λs. λz. s (n s z);
plus = λm. λn. λs. λz. m s (n s z);
times = λm. λn. m (plus n) c0;
iszro = λm. m (λx. fls) tru;
zz = pair c0 c0;
ss = λp. pair (snd p) (plus c1 (snd p));
prd = λm. fst (m ss zz);

tru pass fail;
fls fail pass;

λy.y x;

tru;
test;

test tru v w;

and tru tru;
and tru fls;
and fls tru;
and fls fls;

or tru tru;
or tru fls;
or fls tru;
or fls fls;

not fls;
not tru;

fst (pair v w);
snd (pair v w);

iszro c0;
iszro c1;

scc c0;
plus c2 c3;
times c2 c3;

prd c2;